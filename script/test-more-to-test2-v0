#!/usr/bin/env perl

use 5.010;

use strict;
use warnings;

use File::Glob qw{ bsd_glob };
use File::Spec;
use Getopt::Long 2.33 qw{ :config auto_version };
use App::Test::More::To::Test2::V0;

our $VERSION = '0.000_001';

{
    my $is_win = {
	MSWin32	=> 1,
    };

    use constant CFG_FILE	=> $is_win->{$^O} ?
	'test-more-to-test2-v0.cfg' : 'test-more-to-test2-v0rc';
}

my %opt;

foreach my $cfg_dir (
    [ 'meta' ],
    [],
    [ $ENV{HOME} ],
) {
    my $cfg_path = File::Spec->catfile( @{ $cfg_dir }, CFG_FILE );
    open my $fh, '<:encoding(utf-8)', $cfg_path
	or next;
    my @add_arg;
    while ( <$fh> ) {
	m/ \A \s* [#] /smx
	    and next;
	m/ \S /smx
	    or next;
	s/ \A \s+ //smx;
	s/ \s+ \z //smx;
	push @add_arg, m/ \A - /smx ? $_ : bsd_glob( $_ );
    }
    close $fh;

    my_get_options( @add_arg );
    last;
}

my_get_options( @ARGV );

my @want_files = @{ delete $opt{_want_files} || [] };

@want_files
    or @want_files = bsd_glob( 't/*.t' );

delete $opt{slurp}
    and @want_files = map { slurp( $_ ) } @want_files;

my $app = App::Test::More::To::Test2::V0->new(
    %opt,
    die		=> 1,
);

foreach my $file ( @want_files ) {
    if ( ref $file ) {
	print $app->convert( $file );
    } else {
	$app->convert( $file );
    }
}

sub my_get_options {
    local @ARGV = @_;

    GetOptions( \%opt,
	qw{
	    bail_on_fail|bail-on-fail!
	    explain=s
	    lib=s@
	    load_module|load-module! slurp! suffix=s
	    support_module|support-module=s@
	    support_sub|support-sub=s@
	    uncomment_use|uncomment-use!
	},
	help => sub { pod2usage( { -verbose => 2 } ) },
    ) or pod2usage( { -verbose => 0 } );

    @ARGV
	and $opt{_want_files} = [ @ARGV ];

    return;
}

sub pod2usage {
    my @arg = @_;
    require Pod::Usage;
    Pod::Usage::pod2usage( @arg );
    return;
}

sub slurp {
    my ( $file ) = @_;
    local $/ = undef;
    open my $fh, '<', $file
	or die "Unable to open $file: $!\n";
    my $content = <$fh>;
    close $fh;
    return \$content;
}

__END__

=head1 TITLE

test-more-to-test2-v0 - Convert Test::More tests to Test2::V0

=head1 SYNOPSIS

 test-more-to-test2-v0
 test-more-to-test2-v0 t/test.t
 test-more-to-test2-v0 --help
 test-more-to-test2-v0 --version

=head1 OPTIONS

=head2 --bail-on-fail

If this Boolean option is asserted the script removes calls to
C<BAIL_OUT()>, and installs
L<Test2::Plugin::BailOnFail|Test2::Plugin::BailOnFail>. Otherwise it
just installs its own C<BAIL_OUT()> subroutine.

The default is C<-no-bail-on-fail>.

=head2 --explain

 --explain Data::Dumper=Dumper

This option controls how C<explain()> is converted. The argument is a
string specifying the module to import and the subroutine to use in
place of C<explain()>.

The default is C<--explain Test2::Tools::Explain=explain>.

=head2 --help

This option displays the documentation for this script. The script then
exits.

=head2 --lib

 --lib inc

This option specifies a directory to be search for support modules in
addition to those in C<@INC>. It can be specified more than once.

=head2 --load-module

If this Boolean option is asserted and either C<use_ok()> or
C<require_ok() is encountered,

  use Test2::Tools::LoadModule qw{ :more };

is added to the top of the module to supply them.

Otherwise C<use_ok ... > is converted to C<use ok ...;>, and
C<require_ok $module > is converted to

 ok lives { require $module }, "require $module" or diag <<"EOD"
     Tried to require '$module'
     Error: \$@
 EOD

The diagnostic is omitted if the C<require_ok()> is followed by anything
but C<;>, because the value of
L<Test2::Tools::Basic::diag()|Test2::Tools::Basic> is undocumented.

The default is C<-no-load-module>.

=head2 --slurp

If this Boolean option is asserted, any files specified on the command
line are slurped, and their contents are converted and displayed on
C<STDOUT>. Otherwise the files are converted in-place.

The default is C<--no-slurp>.

=head2 --suffix

If this option is specified and not C<''>, changed files will be backed
up (by renaming) before being rewritten. The name of the backup will be
the name of the original file with the suffix appended.

B<Note> that a leading dot is B<not> implied; if you want F<t/foo.t>
backed up to F<t/foo.t.bak>, you must specify C<--suffix .bak>.

By default, no backup is done, and the original file is overwritten.

=head2 --support-module

 --support-module My::Module::Test

This option specifies a testing support module whose default exports are
to be examined for conflicts with the default exports of
L<Test2::V0|Test2::V0>.

This option can be specified more than once.

=head2 --support-sub

 --support-sub check

This option specifies the name of a testing support subroutine which may
(or may not) conflict with the default exports of
L<Test2::V0|Test2::V0>. This is in addition to subroutines found in the
script being converted and those exported by the modules specified by
L<--support-module|/--support-module>.

This option can be specified more than once.

=head2 --uncomment-use

If this Boolean option is asserted, any comment immediately after and on
the same line as a modified C<use()> or C<no()> statement is removed.
This is to avoid converting something like

 use Test::More 0.88;  # Because of done_testing();

into

 use Test2::V0;  # Because of done_testing();

=head2 --version

This option displays the version of this script. The script then exits.

=head1 DETAILS

This Perl script converts Perl test scripts based on
L<Test::More|Test::More> into scripts based on L<Test2::V0|Test2::V0>.
The file names of scripts to be converted are passed in on the command
line. If none are specified, F<t/*.t> are converted.

For the details of the conversion, see
L<App::Test::More::To::Test2::V0|App::Test::More::To::Test2::V0>,
particularly the documentation of the
L<convert()|App::Test::More::To::Test2::V0/convert> method.

B<Caveat:> Files are modified in-place unless L<--suffix|/--suffix> is
specified.

=head1 CONFIGURATION FILE

You can configure options using a configuration file. The name of this
file is F<test-more-to-test2-v0.cfg> under Windows, and
F<test-more-to-test2v0rc> under any other operating system. The
configuration file can be found either in the F<meta/> subdirectory of
the distribution being converted, in the distribution directory itself,
or in the user's home directory. The first one found will be used.

The configuration file contains command arguments, one per line. Blank
lines and lines whose first non-blank character is C<'#'> will be
ignored. Options and arguments specified on the command line override
those specified in the configuration file.

=head1 AUTHOR

Thomas R. Wyant, III F<wyant at cpan dot org>

=head1 COPYRIGHT AND LICENSE

Copyright (C) 2023 by Thomas R. Wyant, III

This program is free software; you can redistribute it and/or modify it
under the same terms as Perl 5.10.0. For more details, see the full text
of the licenses in the directory LICENSES.

This program is distributed in the hope that it will be useful, but
without any warranty; without even the implied warranty of
merchantability or fitness for a particular purpose.

=cut

# ex: set textwidth=72 :
